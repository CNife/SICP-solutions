# 练习 1.19

定义 ![T_{pq}](https://render.githubusercontent.com/render/math?math=T_%7Bpq%7D) 为对偶 `(a, b)` 按照 ![a\leftarrow bp+a(p+q)](https://render.githubusercontent.com/render/math?math=a%5Cleftarrow%20bp%2Ba(p%2Bq)) 和 ![b\leftarrow bp+aq](https://render.githubusercontent.com/render/math?math=b%5Cleftarrow%20bp%2Baq) 的规则的变换。则斐波那契数可以通过 `p=0`，`q=1` 时应用于对偶 `(1, 0)` `N` 次得出。

请证明，如果应用 ![T_{pq}](https://render.githubusercontent.com/render/math?math=T_%7Bpq%7D) 两次，则与应用一次 ![T_{p'q'}](https://render.githubusercontent.com/render/math?math=T_%7Bp'q'%7D) 的效果相同，其中 `p'` 与 `q'` 可以通过 `p` 和 `q` 计算得出。

请写出基于此变换的过程，在 `N` 的对数步数内计算出第 `N` 项斐波那契数。

---

![(T_{pq})^2](https://render.githubusercontent.com/render/math?math=(T_%7Bpq%7D)%5E2) 对应的变换为

![a\leftarrow(bp+aq)q+\[bq+a(p+q)\](p+q)=b(2pq+q^2)+a(p^2+2q^2+2pq)](https://render.githubusercontent.com/render/math?math=a%5Cleftarrow(bp%2Baq)q%2B%5Bbq%2Ba(p%2Bq)%5D(p%2Bq)%3Db(2pq%2Bq%5E2)%2Ba(p%5E2%2B2q%5E2%2B2pq))

![b\leftarrow(bp+aq)p+\[bq+a(p+q)\]q=b(p^2+q^2)+a(2pq+q^2)](https://render.githubusercontent.com/render/math?math=b%5Cleftarrow(bp%2Baq)p%2B%5Bbq%2Ba(p%2Bq)%5Dq%3Db(p%5E2%2Bq%5E2)%2Ba(2pq%2Bq%5E2))

于是 ![p'=p^2+q^2](https://render.githubusercontent.com/render/math?math=p'%3Dp%5E2%2Bq%5E2)，![q'=q^2+2pq](https://render.githubusercontent.com/render/math?math=q'%3Dq%5E2%2B2pq)。

代码：

```Scheme
(define (fib n)
  (fib-iter 1 0 0 1 n))

(define (fib-iter a b p q n)
  (cond ((= n 0) b)
        ((even? n)
         (fib-iter a
                   b
                   (+ (* p p)
                      (* q q))
                   (+ (* q q)
                      (* 2 p q))
                   (/ n 2)))
        (else
         (fib-iter (+ (* b q)
                      (* a q)
                      (* a p))
                   (+ (* b p)
                      (* a q))
                   p
                   q
                   (- n 1)))))
; 21
(fib 8)
```
